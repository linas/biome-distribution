#! /usr/bin/env gnuplot
#
# Create a graph of the number of tetrahedra found.
#
set term png size 1024,768
set out 'gene-tetra.png'
# set term eps color
# set out 'gene-tetra.eps'

set logscale x
set logscale y
set format y "10^{%L}"
set title "Tetrahedra per gene (non-symmetrized)"
set xlabel "Gene Rank (ranked by count of tetrahedra)"
set ylabel "Number of tetrahedra"
set key left bottom

set yrange [1e-8:2e-2]

ntetra = 55297584
overcnt = 16

sa = 1.3e-2
sb = 3

obeta = 2e5

# Eyeballed fit to the data.
plot "gene-tetra.csv" using 1:($3 / (ntetra * overcnt)) with lines title "data", \
     "gene-tetra.csv" using 1:(sa / ($1)**0.25) with lines title "A / rank^{1/4}", \
     "gene-tetra.csv" using 1:(sb / ($1)**1.5) with lines title "B / rank^{3/2}", \
     "gene-tetra.csv" using 1:((sb / ($1)**1.5) * exp(-($1)**1.5 / obeta)) with lines title "B exp(-rank^{3/2}/2e5) / rank^{3/2}"

