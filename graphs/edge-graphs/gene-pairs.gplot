#! /usr/bin/env gnuplot
#
# Create a graph of the number of edges found.
#
# set term png size 1024,768
# set out 'gene-pairs.png'
set term eps color
set out 'gene-pairs.eps'

set logscale x
set logscale y
set format y "10^{%L}"
set title "Interactions per gene (symmetrized)"
set xlabel "Gene Rank (ranked by count of interactions)"
set ylabel "Fraction of interactions"

set yrange [1e-6:]

# Number of edges, grand-total.
nedges = 365745.0

# Due to double-counting, each vertex got quadruple-counted, so
norm = 1 / (4 * nedges)

# Leading term - this is 2 gamma(1/3) (2N)^{1/3} for N=nedges
lead = 1 / 160.914

# Eyeballed fit to the data.
plot "gene-pairs.csv" using 1:(norm * $3) with lines lw 3 title "data", \
     "gene-pairs.csv" using 1:(lead / ($1)**0.5) with lines lw 3 title "A rank^{-1/2}", \
     "gene-pairs.csv" using 1:((lead / ($1)**0.5) * exp(-($1)**1.5 / (2*nedges))) with lines lw 3 title "A exp(-rank^{3/2}/ 2N) rank^{-1/2}"

#     "gene-pairs.csv" using 1:((4*lead / ($1)**0.5) * exp(-($1) / 9500)) with lines title "9600 exp(-rank/9500)/âˆšrank", \
