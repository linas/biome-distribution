#! /usr/bin/env gnuplot
#
# Create a graph of the number of loops found.
#
set term png
set out 'tri-weighted-mi.png'

set title "Probability of gene pair having given MI"
set xlabel "Mutual Information"
set ylabel "Weighted probability of a gene pair"
set xrange [-2:20]
set yrange [1e-5:0.25]
set logscale y
set format y "10^{%L}"

# There were 455572 gene pairs that were observed 5050388 times, total.
# These were binned into 300 bins.  The range was from MI=-5 to +25,
# so that the bin-width is (30/300). The normalization is thus the
# total-count divided by bin width, so that the integral under the
# curve is 1.0
norm = 300/(30 * 5050388.0)
C = 1 / sqrt(2 * 3.1415926)
plot "tri-weighted-mi.csv" using 2:($3*norm) with lines title "data", \
     "tri-weighted-mi.csv" using 2:(0.5 * C * exp(-0.95*($2-1.6)**2)) with lines title "C/2 exp(-0.95 (MI-1.6)^2)", \
     "tri-weighted-mi.csv" using 2:(0.5 *C* exp(-0.023*($2**2))) with lines title "C/2 exp(-0.023 MI^2)",\
     "tri-weighted-mi.csv" using 2:(1) title "C = 1/√ 2 π"
